--Latest Care Contact data (based on table [MHS201CareContact])

CREATE VIEW [dbo].[v_SF_Latest_CareContact]
AS

WITH A as
(
SELECT CAST([SK] as varchar(500)) as [SK]
		,[CareContactId]
		,[ServiceRequestId]
		,[CareProfTeamLocalId]
		,[CareContDate]
		,[CareContTime]
		,[OrgIDComm]
		,[AdminCatCode]
		,[SpecialisedMHServiceCode]
		,CAST([ClinContDurOfCareCont] as [int]) as [ClinContDurOfCareCont]
		,[ConsType]
		,[CareContSubj]
		,[ConsMediumUsed]
		,[ActLocTypeCode]
		,[PlaceOfSafetyInd]
		,[SiteIDOfTreat]
		,[GroupTherapyInd]
		,[AttendOrDNACode]
		,[EarliestReasonOfferDate]
		,[EarliestClinAppDate]
		,[CareContCancelDate]
		,[CareContCancelReas]
		,[RepApptOfferDate]
		,[RepApptBookDate]
		,[RecordNumber]
		,[MHS201UniqID]
		,[OrgIDProv]
		,CAST([Person_ID] as varchar(100)) as [Person_ID]
		,[UniqSubmissionID]
		,[UniqServReqID]
		,[UniqCareContID]
		,CAST([AgeCareContDate] as int) as [AgeCareContDate]
		,CAST([ContLocDistanceHome] as int) as [ContLocDistanceHome]
		,CAST([TimeReferAndCareContact] as int) as [TimeReferAndCareContact]
		,[UniqCareProfTeamID]
		,[UniqMonthID]
		,[EFFECTIVE_FROM]
		,[dmicImportLogId]
		,[dmicMinContactDate]
		,[dmicMaxContactDate]
		,[dmicSystemId]
		,[dmicCCGCode]
		,[dmicDateAdded]
		,[UniqMHSDSPersID]
		,[FileType]
		,[ReportingPeriodStartDate]
		,[ReportingPeriodEndDate]
		,[dmicDataset]
FROM [mhsds].[MHS201CareContact]

UNION

SELECT [SK]
		,[CareContactId]
		,[ServiceRequestId]
		,[CareProfTeamLocalId]
		,CAST([CareContDate] as date) as [CareContDate]
		,CAST([CareContTime] as time) as [CareContTime]
		,[OrgIDComm]
		,[AdminCatCode]
		,[SpecialisedMHServiceCode]
		,[ClinContDurOfCareCont]
		,[ConsType]
		,[CareContSubj]
		,[ConsMediumUsed]
		,[ActLocTypeCode]
		,[PlaceOfSafetyInd]
		,[SiteIDOfTreat]
		,[GroupTherapyInd]
		,[AttendOrDNACode]
		,CAST([EarliestReasonOfferDate] as date) as [EarliestReasonOfferDate]
		,CAST([EarliestClinAppDate] as date) as [EarliestClinAppDate]
		,CAST([CareContCancelDate] as date) as [CareContCancelDate]
		,[CareContCancelReas]
		,CAST([RepApptOfferDate] as date) as [RepApptOfferDate]
		,CAST([RepApptBookDate] as date) as [RepApptBookDate]
		,CAST([RecordNumber] as varchar(19)) as [RecordNumber]
		,CAST([MHS201UniqID] as varchar(19)) as [MHS201UniqID]
		,[OrgIDProv]
		,[Person_ID]
		,CAST([UniqSubmissionID] as varchar(6)) as [UniqSubmissionID]
		,[UniqServReqID]
		,[UniqCareContID]
		,[AgeCareContDate]
		,[ContLocDistanceHome]
		,[TimeReferAndCareContact]
		,[UniqCareProfTeamID]
		,[month_id] as [UniqMonthID]
		,NULL as [EFFECTIVE_FROM]
		,NULL as [dmicImportLogId]
		,[IC_Min_Contact_Date] as [dmicMinContactDate]
		,[IC_Max_Contact_Date] as [dmicMaxContactDate]
		,NULL as [dmicSystemId]
		,[dmicCCG] as [dmicCCGCode]
		,[DMIC_InsertedDate] as [dmicDateAdded]
		,[UniqMHSDSPersID]
		,[FILE TYPE] as [FileType]
		,[Reporting Period Start Date] as [ReportingPeriodStartDate]
		,[Reporting Period End Date] as [ReportingPeriodEndDate]
		,[dmicDataset]
FROM [mhsds_v4].[MHS201CareContact]
WHERE IC_USE_Submission_Flag is null or IC_USE_Submission_Flag = 'Y'
),
B as
(
Select ROW_NUMBER() OVER (PARTITION BY [UniqServReqID], [UniqCareProfTeamID], [UniqCareContID] ORDER BY [ReportingPeriodEndDate] DESC, [FileType] DESC, dmicCCGCode desc) AS [RecPriority]
	,*
FROM A
)
select [SK]
		,[CareContactId]
		,[ServiceRequestId]
		,[CareProfTeamLocalId]
		,[CareContDate]
		,[CareContTime]
		,[OrgIDComm]
		,[AdminCatCode]
		,[SpecialisedMHServiceCode]
		,[ClinContDurOfCareCont]
		,[ConsType]
		,[CareContSubj]
		,[ConsMediumUsed]
		,[ActLocTypeCode]
		,[PlaceOfSafetyInd]
		,[SiteIDOfTreat]
		,[GroupTherapyInd]
		,[AttendOrDNACode]
		,[EarliestReasonOfferDate]
		,[EarliestClinAppDate]
		,[CareContCancelDate]
		,[CareContCancelReas]
		,[RepApptOfferDate]
		,[RepApptBookDate]
		,[RecordNumber]
		,[MHS201UniqID]
		,[OrgIDProv]
		,B.[Person_ID]
		,[UniqSubmissionID]
		,[UniqServReqID]
		,[UniqCareContID]
		,[AgeCareContDate]
		,[ContLocDistanceHome]
		,[TimeReferAndCareContact]
		,[UniqCareProfTeamID]
		,[UniqMonthID]
		,[EFFECTIVE_FROM]
		,B.[dmicImportLogId]
		,[dmicMinContactDate]
		,[dmicMaxContactDate]
		,NULL as [dmicSystemId]
		,[dmicCCGCode]
		,[dmicDateAdded]
		,[UniqMHSDSPersID]
		,[FileType]
		,[ReportingPeriodStartDate]
		,[ReportingPeriodEndDate]
		,[dmicDataset]
		,[Pseudo_NHS_Number] =  case
								WHEN coalesce(n1.[Pseudo_NHSNumber],n2.[Pseudo_NHS_Number],n3.[Pseudo_NHS_Number]) = '' THEN NULL
								ELSE coalesce(n1.[Pseudo_NHSNumber],n2.[Pseudo_NHS_Number],n3.[Pseudo_NHS_Number])
							END 
from B
	left outer join [mhsds].[Bridging] n1
		on n1.Person_Id = B.Person_ID
	left outer join [mhsds_v4].[Bridging] n2
		on n2.Person_Id = coalesce(B.Person_ID,B.UniqMHSDSPersID)
	left outer join [mhsds_v3].[Bridging] n3
		on n3.Person_Id = coalesce(B.Person_ID,B.UniqMHSDSPersID)
WHERE [RecPriority] = 1

